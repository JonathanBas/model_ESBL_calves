model {

	# INITIAL CARRIAGE
	
	for (indiv in init_pos){
		portage_obs[indiv,1] <- 1
	}
	
	for (indiv in init_neg){
		portage_obs[indiv,1] <- 0
	}
	
	# BETWEEN WEEKS 2 AND 48
	
	for (t in 2:48){
		for (indiv in 1:N_calves){
			prob_equal_1[indiv,t] <- ifelse(portage_obs[indiv,t-1] == 0,
											1-exp(-(beta_b[farm_bel[indiv,4]] * sum(portage_obs[,t-1] * farm_bel[, farm_bel[indiv,4]]) / 15)* alpha_d_Pe^(max(0, 1 - last_expo_class[indiv, t, 1]*tau)) * alpha_d_Po^(max(0, 1 - last_expo_class[indiv, t, 2]*tau)) * alpha_d_T^(max(0, 1 - last_expo_class[indiv, t, 3]*tau)) * alpha_d_M^(max(0, 1 - last_expo_class[indiv, t, 4]*tau)) * alpha_d_ST^(max(0, 1 - last_expo_class[indiv, t, 5]*tau))),
											exp(-nu_0))
			
			portage_obs[indiv,t] ~ dbern(max(0.00001, prob_equal_1[indiv,t]))
		}
	}
	
	# BETWEEN WEEKS 49 AND 55
	
	for (t in 49:55){
		for (indiv in 1:N_calves){
			prob_equal_1[indiv,t] <- ifelse(portage_obs[indiv,t-1] == 0,
											1-exp(-(beta_b[farm_bel[indiv,4]] * sum(portage_obs[,t-1] * farm_bel[, farm_bel[indiv,4]] * (1 - box_bel[, box_bel[indiv,10]])) / sum(farm_bel[, farm_bel[indiv,4]] * (1 - box_bel[, box_bel[indiv,10]]))  + beta_w[farm_bel[indiv,4]] * sum(portage_obs[,t-1] * box_bel[, box_bel[indiv,10]]) / sum(box_bel[, box_bel[indiv,10]]))* alpha_d_Pe^(max(0, 1 - last_expo_class[indiv, t, 1]*tau)) * alpha_d_Po^(max(0, 1 - last_expo_class[indiv, t, 2]*tau)) * alpha_d_T^(max(0, 1 - last_expo_class[indiv, t, 3]*tau)) * alpha_d_M^(max(0, 1 - last_expo_class[indiv, t, 4]*tau)) * alpha_d_ST^(max(0, 1 - last_expo_class[indiv, t, 5]*tau))),
											exp(-nu_0))
			
			portage_obs[indiv,t] ~ dbern(max(0.00001, prob_equal_1[indiv,t]))
		}
	}
	
	# BETWEEN WEEKS 56 and END (161)
	
	for (t in 56:t_end){
		for (indiv in from_56){
			prob_equal_1[indiv,t] <- ifelse(portage_obs[indiv,t-1] == 0,
											1-exp(-(beta_b[farm_bel[indiv,4]] * sum(portage_obs[from_56,t-1] * farm_bel[from_56, farm_bel[indiv,4]] * (1 - box_bel[from_56, box_bel[indiv,10]])) / sum(farm_bel[from_56, farm_bel[indiv,4]] * (1 - box_bel[from_56, box_bel[indiv,10]]))  + beta_w[farm_bel[indiv,4]] * sum(portage_obs[from_56,t-1] * box_bel[from_56, box_bel[indiv,10]]) / sum(box_bel[from_56, box_bel[indiv,10]]))* alpha_d_Pe^(max(0, 1 - last_expo_class[indiv, t, 1]*tau)) * alpha_d_Po^(max(0, 1 - last_expo_class[indiv, t, 2]*tau)) * alpha_d_T^(max(0, 1 - last_expo_class[indiv, t, 3]*tau)) * alpha_d_M^(max(0, 1 - last_expo_class[indiv, t, 4]*tau)) * alpha_d_ST^(max(0, 1 - last_expo_class[indiv, t, 5]*tau))),
											exp(-nu_0))
			
			portage_obs[indiv,t] ~ dbern(max(0.00001, prob_equal_1[indiv,t]))
		}
	}
	
	# PRIORS
	
	nu_0 ~ dunif(0,10)
	
	beta_w_f1 ~ dunif(0,10)
	beta_w_f2 ~ dunif(0,10)
	beta_w_f3 ~ dunif(0,10)
	beta_w[1] <- beta_w_f1
	beta_w[2] <- beta_w_f2
	beta_w[3] <- beta_w_f3
	
	beta_b_f1 ~ dunif(0,10)
	beta_b_f2 ~ dunif(0,10)
	beta_b_f3 ~ dunif(0,10)
	beta_b[1] <- beta_b_f1
	beta_b[2] <- beta_b_f2
	beta_b[3] <- beta_b_f3
		
	tau ~ dunif(0,1)
	alpha_d_Pe ~ dunif(0,10)
	alpha_d_Po ~ dunif(0,10)
	alpha_d_T ~ dunif(0,10)
	alpha_d_M ~ dunif(0,10)
	alpha_d_ST ~ dunif(0,10)
}
